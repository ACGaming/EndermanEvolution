buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.12.2-" + getVersion("VERSION")
group = "p455w0rd"
archivesBaseName = "EndermanEvolution"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.12.2-14.23.0.2546"
	runDir = "run"
	mappings = "snapshot_20171003"
	useDepAts = true
}

repositories {
	mavenCentral()
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "tterrag"
		url = "http://maven.tterrag.com"
	}
	maven {
		name = "tehnut"
		url = "http://tehnut.info/maven"
	}
	maven {
		name = "ChickenBones"
		url "http://chickenbones.net/maven"
	}
}

dependencies {
	deobfCompile "mezz.jei:jei_1.12.2:+"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.20-B35_1.12"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.8-2"
	deobfCompile "codechicken:CodeChickenLib:1.12-3.1.3.313:universal"
	deobfCompile "codechicken:EnderStorage:1.12-2.4.1.124:universal"
	deobfCompile "cpw.mods:ironchest:1.12.2-7.0.34.820"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
		
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
	    attributes 'FMLAT': 'endermanevo_at.cfg'
	}
}

String getVersion(String type) {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String $type = \"";
	File file = file("src/main/java/p455w0rd/endermanevo/init/ModGlobals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
